buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.22.0'
    }
}

plugins {
    id 'net.researchgate.release' version '3.0.0'
    id 'java'
    id  'maven-publish'
    id 'signing'
    id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: "com.vanniktech.maven.publish"
    group = 'fr.maif'

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    repositories {
        mavenCentral()
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }
    ext {
        jacksonVersion = "2.10.5"
        akkaVersion = "2.6.6"
        vavrVersion = "0.10.0"
        _jooqVersion = "3.17.4"
        scalaVersion = "2.13"
    }
    test {
        testLogging {
            events "PASSED", "SKIPPED", "FAILED", "STANDARD_OUT", "STANDARD_ERROR"
        }
    }

    tasks.named('test') {
        useJUnitPlatform()
    }


    publishing {
        publications {
            mavenJava(MavenPublication) {
                from(components.java)
                artifact sourcesJar
                pom {
                    groupId = 'fr.maif'
                    name = 'jooq-async'
                    description = ''
                    url = 'https://github.com/maif/jooq-async'
                    inceptionYear = '2020'

                    licenses {
                        license {
                            name = 'Apache-2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0'
                        }
                    }
                    developers {
                        developer {
                            id = 'alexandre.delegue'
                            name = 'Alexandre Del√®gue'
                            url = 'https://github.com/larousso'
                        }
                        developer {
                            id = 'benjamin.cavy'
                            name = 'Benjamin Cavy'
                            url = 'https://github.com/ptitFicus'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/maif/jooq-async.git'
                        developerConnection = 'scm:git:ssh://github.com/maif/jooq-async.git'
                        url = 'https://github.com/maif/jooq-async'
                    }
                }
            }
        }
        repositories {
            maven {
                def isReleaseVersion = !version.endsWith("-SNAPSHOT")
                def releaseRepo = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snaphostRepo = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                name = "OSSRH"
                url = isReleaseVersion ? releaseRepo : snaphostRepo //"https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = System.env.SONATYPE_USERNAME
                    password = System.env.SONATYPE_PASSWORD
                }
            }
        }
    }
}

release {
    tagTemplate = 'v${version}'
}
release.git.requireBranch.set('master')